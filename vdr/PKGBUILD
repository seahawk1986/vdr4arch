#Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr
pkgver=2.0.0
pkgrel=1
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-dejavu')
optdepends=('netstat-nat: check IP connections for lifeguard-shutdown-script'
            'vdr-dbus2vdr: display shutdown-messages'
            'lirc: remote control support'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=2.0.0")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf'
	'var/lib/vdr/lifeguard.conf')
options=(!emptydirs)
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}.tar.bz2"
        'MainMenuHooks-v1_0_2.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=30330'
        'shutdown.sh'
        'lifeguard.conf'
        'vdr-addon-lifeguard.1'
        'shutdown-wrapper.c'
	'remotetimers.h'
        'vdr-1.7.29-skinlcars.patch'
	'http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-2.0.0-menuselection.patch.gz'
	'http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-2.0.0-resetresume.patch.gz'
        'http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-2.0.0-filetransfer.patch.gz'
	'menuorg.diff'
	'vdr-1.7.40-dynamite.patch'
	'http://ein-eike.de/wordpress/wp-content/uploads/2013/03/vdr-patch-for-permashift-0.5.2.patch'
        )

md5sums=('fd7f481b996e03fae3c00e80b6b0d301'
         '301c9b9766ed5182b07f1debc79abc21'
         '5dc29e2434c0353a3ebf2d5eb663527a'
         '0b7692a8e9017952c392b0a334226148'
         '1e1a66cc6a6502a59555d37fd7c8f4d4'
         '70ab46210425bcc2606c47cd6206f224'
         '5a04f6caecd2426fe624ea74849bb54e'
         '2fca23b247aa393bbb4b52e53a75581e'
         'e85a6fd72edbe5beb83a3c8ba08a6b2d'
         'fafa37f7cc013f86c92443ced9405e40'
         '61a17c00e0794084627244d4a7aaa5b4'
         'b1c3fa683891c96e36b2357b3de2c244'
         '6386c810adb20a96ee8d75c06042f13c'
         '8ab77b6b4463f8c359c3ba29336a0e96')

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
CFLAGS      += -g -O3
CXXFLAGS    += -g -O3
PREFIX       = /usr
LIBDIR       = /usr/lib/vdr/plugins
VDR_USER     = vdr
EOF

  patch -p1 -i ${srcdir}/MainMenuHooks-v1_0_2.diff
  patch -p1 -i ${srcdir}/vdr-2.0.0-menuselection.patch
  patch -p1 -i ${srcdir}/vdr-2.0.0-resetresume.patch
  patch -p1 -i ${srcdir}/menuorg.diff
  mkdir -p PLUGINS/src/remotetimers/
  cp ${srcdir}/remotetimers.h PLUGINS/src/remotetimers/
  patch -p1 -i ${srcdir}/vdr-1.7.29-skinlcars.patch
  patch -p1 -i ${srcdir}/vdr-1.7.40-dynamite.patch
  patch -p1 -i ${srcdir}/vdr-patch-for-permashift-0.5.2.patch
  make
}

package() {
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"
  mkdir -p "$pkgdir/var/lib/vdr/"
  mkdir -p "$pkgdir/usr/share/man/man1/"
  install -Dm644 lifeguard.conf  "$pkgdir/var/lib/vdr/"
  install -Dm644 vdr-addon-lifeguard.1  "$pkgdir/usr/share/man/man1/"

  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=runvdr-extreme
pkgver=0.4.2
pkgrel=6
pkgdesc="Runvdr script, just like the runvdr script included in the VDR distribution"
url="http://www.udo-richter.de/vdr/scripts.html"
arch=('any')
license=('GPL2')
depends=('bash' 'vdr')
optdepends=('xorg-xinit: For fireing up a X session')
backup=(etc/runvdr.conf)
source=("http://www.udo-richter.de/vdr/files/${pkgname}-${pkgver}.tgz"
        '60-vdr-tty.rules'
        'runvdr.conf'
        'runvdr-extreme-0.4.2-bash-4.2-fix.diff'
        'runvdr-extreme-0.4.2-charset-override.diff'
        'vdr.service')
md5sums=('ca95ce455fc6e66fcaa4b3476ecdcffc'
         '32dcab4ce46f8164977ab3abdeccec57'
         '64f49477357db1391b578dfb91b82fbc'
         '0004d23d3719abe9ce54ce47c32d27d3'
         '5ca14709ba9064a745d6162c04e73630'
         'a87b3399675a5493637a7f115475b3f2')

package() {
  install -Dm644 60-vdr-tty.rules "$pkgdir/usr/lib/udev/rules.d/60-vdr-tty.rules"
  install -Dm755 vdr.service "$pkgdir/usr/lib/systemd/system/vdr.service"
  install -Dm644 runvdr.conf "$pkgdir/etc/runvdr.conf"

  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -i ${srcdir}/runvdr-extreme-0.4.2-bash-4.2-fix.diff
  patch -i ${srcdir}/runvdr-extreme-0.4.2-charset-override.diff #By Manuel Reimer

  sed -i -e 's/VDRPRG=$/VDRPRG=vdr/g' runvdr

  install -Dm755 runvdr "$pkgdir/usr/bin/runvdr"
}

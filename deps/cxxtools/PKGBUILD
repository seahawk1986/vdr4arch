# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=cxxtools
pkgver=2.1.1
pkgrel=3
pkgdesc="Collection of general-purpose C++ classes"
url="http://www.tntnet.org"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('bash' 'gcc-libs')
options=(!libtool)
source=("http://www.tntnet.org/download/${pkgname}-${pkgver}.tar.gz" "fix-utf8.diff")
md5sums=('2026a2bb23b966f13893167b4dbc5d70'
         '5a23ef9da9b54da239ab8e89521c8239')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/fix-utf8.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --disable-demos \
              --disable-unittest
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

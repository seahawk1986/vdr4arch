# Maintainer: Alexander Grothe <seahawk1986[at]hotmail[dot]com>
pkgname=vdr-markad
pkgver=20130301
_gitver=f68a8728b
_vdrapi=1.7.40
pkgrel=2
pkgdesc="automatic advertisement detection"
url="http://projects.vdr-developer.org/projects/plg-markad"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('ffmpeg' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/git/vdr-plugin-markad.git/snapshot/vdr-plugin-${_plugname}-${_gitver}.tar.bz2")
md5sums=('c143d08dc94c5a7b2692513d62e29892')

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${_gitver}"

  mkdir -p $pkgdir/usr/lib/vdr/plugins
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       DESTDIR=$pkgdir install

  #Move the logofiles to the right place and add a symlink to workaround patching.
  mkdir -p "$pkgdir/usr/share"
  mv "$pkgdir/var/lib/markad" "$pkgdir/usr/share/markad"
  ln -s /usr/share/markad "$pkgdir/var/lib/markad"
}

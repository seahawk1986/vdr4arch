# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-dvbapi
pkgver=1.0.4_4_g555272d
epoch=1
_gitver=555272d2
_vdrapi=2.0.0
pkgrel=1
pkgdesc="A bridge between VDR and OScam."
url="https://github.com/manio/vdr-plugin-dvbapi"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' 'libdvbcsa' 'oscam' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://github.com/manio/vdr-plugin-dvbapi.git#commit=$_gitver")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make LIBDVBCSA=1
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make LIBDVBCSA=1 DESTDIR="$pkgdir" install
}

# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-xineliboutput
pkgver=1.1.0
_gitver=32bde0a1eb887de10e460fe12c32251b6a7f1114
_vdrapi=2.0.0
pkgrel=2
pkgdesc="X11 and Linux framebuffer front-end for VDR"
url="http://www.sourceforge.net/projects/xineliboutput"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('dbus-glib' 'libextractor' "vdr-api=${_vdrapi}" 'xine-lib')
optdepends=('nvidia: Required for VDPAU decoding'
            'xorg-server: Required for software decoding')
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/$_plugname.git#commit=$_gitver"
        'xineliboutput-vdr2.1.2compat.diff')
md5sums=('SKIP'
         'd313b50c576ab17dfb2a08d50981bec8')

pkgver() {
  cd "${srcdir}/${_plugname}"
  git describe --tags | sed "s/$_plugname.//g;s/_/./g;s/-/./g"
}

prepare() {
  cd "${srcdir}/${_plugname}"
  patch -p1 -i "$srcdir/xineliboutput-vdr2.1.2compat.diff"
}

package() {
  cd "${srcdir}/${_plugname}"

  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       DESTDIR="$pkgdir" \
       install
}

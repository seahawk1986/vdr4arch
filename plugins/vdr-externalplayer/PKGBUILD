# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
pkgname=vdr-externalplayer
pkgver=0.2.0
_vdrapi=1.7.37
pkgrel=2
pkgdesc="use external players in VDR"
url="http://sourceforge.net/projects/externalplayer/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
backup=('var/lib/vdr/plugins/externalplayer.conf')
install="${pkgname}.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
replaces=("vdr-plugin-$_plugname")
conflicts=("vdr-plugin-$_plugname")
source=("http://www.uli-eckhardt.de/vdr/download/${pkgname}-${pkgver}.tgz"
        'makefilefix.diff')
md5sums=('0ad6ebeaedef66aac9564ba437c1f4e9'
         '2170044e62b864e759c64ffe66f44f34')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  patch -p1 -i ${srcdir}/makefilefix.diff

  make
}
package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -Dm644 examples/externalplayer.conf "$pkgdir/var/lib/vdr/plugins/externalplayer.conf"
}

# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-sc
pkgver=20120721
_hgver=29b7b5f2
_vdrapi=2.0.0
pkgrel=1
pkgdesc="Software CAM emulation"
url="http://207.44.152.197/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('openssl' "vdr-api=${_vdrapi}")
makedepends=('mercurial')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("$pkgname::hg+http://85.17.209.13:6100#revision=$_hgver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

prepare() {
  cd "$srcdir/$pkgname"

  #Disable Keyfile support. If you want to watch paytv, pay for it.
  ( cd systems
    ls | grep -v cardclient | xargs rm -r )
}

package() {
  cd "$srcdir/$pkgname"

  if [ "${CARCH}" = "x86_64" ]; then
    CPUOPT='x86-64'
    PARALLEL=PARALLEL_128_SSE2
  else
    CPUOPT="${CARCH}"
    PARALLEL=PARALLEL_32_INT
  fi

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       CPUOPT="$CPUOPT" \
       PARALLEL="$PARALLEL" \
       MAXCAID=64 \
       CSAFLAGS="$(pkg-config vdr --variable=cflags) -funroll-loops" \
       i18n systems libvdr-$_plugname.so
  install -Dm755 libvdr-$_plugname.so "$pkgdir/usr/lib/vdr/plugins/libvdr-$_plugname.so.$_vdrapi"
}

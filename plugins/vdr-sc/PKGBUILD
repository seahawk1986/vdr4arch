# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-sc
pkgver=20120721
_hgver=29b7b5f2
_vdrapi=2.0.0
pkgrel=2
pkgdesc="Software CAM emulation"
url="http://207.44.152.197/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libdvbcsa' 'openssl' "vdr-api=${_vdrapi}")
makedepends=('mercurial')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("$pkgname::hg+http://85.17.209.13:6100#revision=$_hgver"
        'Update_to_new_Makefile.diff'
        'Add_libdvbcsa_support.diff')
md5sums=('SKIP'
         'bc27c68139feac6cdc316af0075522c4'
         '4df41f14963f1c4a6c52d9fd0400ae79')

pkgver() {
  cd "$srcdir/$pkgname"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

prepare() {
  cd "$srcdir/$pkgname"

  patch -p1 -i "$srcdir/Update_to_new_Makefile.diff"
  patch -p1 -i "$srcdir/Add_libdvbcsa_support.diff"

  #Disable Keyfile support. If you want to watch paytv, pay for it.
  ( cd systems
    ls | grep -v cardclient | xargs rm -r )
}

build() {
  cd "$srcdir/$pkgname"
  make LIBDVBCSA=1
}

package() {
  cd "$srcdir/$pkgname"
  make LIBDVBCSA=1 DESTDIR="$pkgdir" install
}

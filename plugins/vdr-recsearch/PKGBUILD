# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-recsearch
pkgver=0.0.8
_vdrapi=2.0.0
pkgrel=1
pkgdesc="Search your recordings and find the one you are looking for"
url="https://github.com/flensrocker/vdr-plugin-recsearch"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("https://github.com/flensrocker/vdr-plugin-$_plugname/archive/v$pkgver.tar.gz"
        'recsearch-vdr2.1.2compat.diff')
md5sums=('fbdc96326245d53dbd46fe3bce68b8d5'
         '8ba565731d9cb506167e822ad5f5bc44')

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/recsearch-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
